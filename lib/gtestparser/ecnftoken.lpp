%{
#include <iostream>
#include <string>
#include <sstream>
using namespace std;

#include "parser.hh"

stringstream ss;
stringstream results;
stringstream error;
%}

/* lex definitions: */
%option noyywrap never-interactive nounput

%s block
%s resultblock

INT				[0-9]+

%%

<*>"[ RUN      ] "		{ BEGIN(block); error <<ss.str(); ss.str("");}
<*>"[EXECUTABLE] "[^\n]*	{ results <<yytext <<"\n";}
<*>"[  PASSED  ] "		{ BEGIN(resultblock); error <<ss.str(); ss.str(""); results <<"Passed "; }
<*>"  YOU HAVE "{INT}" DISABLED TESTS" { results <<yytext <<"\n"; }
<*>"[==========] "		{ BEGIN(INITIAL); error <<ss.str(); ss.str(""); }
<*>"[----------] "		{ BEGIN(INITIAL); error <<ss.str(); ss.str(""); }
<block>"[  FAILED  ] "	{ BEGIN(INITIAL); yylval.str = new string(ss.str()); ss.str(""); return FAIL; }
<block>"[       OK ] " 	{ BEGIN(INITIAL); yylval.str = new string(ss.str()); ss.str(""); return SUCCESS; }
<block>[^\[\n]*			{ ss <<yytext; }
<block>\n				{ ss <<"\n"; }
<resultblock>"[  FAILED  ] "	{ results <<yytext; }
<resultblock>"\n\n"		{ BEGIN(INITIAL); results <<ss.str() <<"\n\n"; ss.str(""); }
<resultblock>"\n"		{ results <<ss.str() << "\n"; ss.str("");}
<resultblock>[^\[\n]*	{ ss <<yytext; }
<*>[^\[\n]*				{  }
<*>.					{}
<INITIAL>\n				{  }
